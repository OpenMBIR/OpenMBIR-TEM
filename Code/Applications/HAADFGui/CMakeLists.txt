#--////////////////////////////////////////////////////////////////////////////
#--
#--  Copyright (c) 2011, Michael A. Jackson. BlueQuartz Software

#--  All rights reserved.
#--  BSD License: http://www.opensource.org/licenses/bsd-license.html
#--
#-- This code was partly written under US Air Force Contract FA8650-07-D-5800
#--
#--////////////////////////////////////////////////////////////////////////////
Project(HAADFGui)
cmake_minimum_required(VERSION 2.8.6)

# Set the GUI type for different platforms
SET(GUI_TYPE )


# Set some Win32 Specific Settings
IF(WIN32)
 SET(GUI_TYPE WIN32)
ENDIF(WIN32)

# Set some include directories
include_directories( ${PROJECT_CODE_DIR} )
include_directories( ${PROJECT_CODE_DIR}/Applications )
include_directories( ${PROJECT_CODE_DIR}/Applications/HAADFGui )
include_directories( ${PROJECT_CODE_DIR}/QtSupport )
include_directories( ${HAADFGui_BINARY_DIR} )
include_directories( ${OpenMBIR_BINARY_DIR}/QtSupport )


set(CLASS_NAME "HAADF_MultiResolutionReconstruction")
SET(ENGINE_INCLUDE_PATH "MBIRLib/HAADF")
configure_file( ${PROJECT_CODE_DIR}/QtSupport/QObserverWrapper.h.in
                ${HAADFGui_BINARY_DIR}/Q${CLASS_NAME}.h @ONLY IMMEDIATE)

# --------------------------------------------------------------------
# Set up all the source codes
SET( HAADFGui_SRCS
  ${HAADFGui_SOURCE_DIR}/main.cpp
  ${HAADFGui_SOURCE_DIR}/HAADFGui.cpp
)

# another list, this time it includes all header files that should
#  be treated with moc
SET( HAADFGui_MOC_HDRS
  ${HAADFGui_SOURCE_DIR}/HAADFGui.h
)
cmp_IDE_SOURCE_PROPERTIES(HAADFGui "${HAADFGui_MOC_HDRS}" "${HAADFGui_SRCS}" "0")



# The UI files (As generated by Qt Designer
SET( HAADFGui_UIS
 ${HAADFGui_SOURCE_DIR}/UI_Files/HAADFGui.ui
)
cmp_IDE_SOURCE_PROPERTIES(HAADFGui/UI_Files "${HAADFGui_UIS}" "" "0")

# Organize the .qrc files that were generated from the various license files
cmp_IDE_GENERATED_PROPERTIES(HAADFGui/Generated/License "${QRC_LICENSE_FILES}" "" "0")

# and finally any resource files
SET( HAADFGui_RCS
 ${PROJECT_RESOURCES_DIR}/icons/images/Icons.qrc
)
cmp_IDE_SOURCE_PROPERTIES(HAADFGui/QrcFiles "${HAADFGui_RCS}" "" "0")

# -- Run MOC and UIC on the necessary files
QT4_ADD_RESOURCES( Generated_RC_SRCS ${HAADFGui_RCS}  ${QRC_LICENSE_FILES})

# this will run uic on .ui files:
QT4_WRAP_UI( Generated_UI_HDRS ${HAADFGui_UIS} )

# and finally this will run moc:
QT4_WRAP_CPP( Generated_MOC_SRCS ${HAADFGui_MOC_HDRS} ${HAADFGui_BINARY_DIR}/Q${CLASS_NAME}.h )

#-- Put the generated files into their own group for IDEs
cmp_IDE_GENERATED_PROPERTIES(HAADFGui/Generated "${Generated_UI_HDRS};${HAADFGui_BINARY_DIR}/Q${CLASS_NAME}.h" "${Generated_MOC_SRCS};${Generated_RC_SRCS}")


# -- Finally set the Project Sources to include all the sources
SET (${PROJECT_NAME}_PROJECT_SRCS
  ${HAADFGui_SRCS}
  ${HAADFGui_MOC_HDRS}
  ${HAADFGui_HDRS}
  ${Generated_RC_SRCS}
  ${Generated_UI_HDRS}
  ${Generated_MOC_SRCS}
)

IF(APPLE)
    set(APPLICATION_ICON ${PROJECT_RESOURCES_DIR}/icons/icns/${PROJECT_NAME}.icns)
ENDIF (APPLE)

IF(Q_WS_WIN)
    set(APPLICATION_ICON ${PROJECT_RESOURCES_DIR}/icons/ico/${PROJECT_NAME}.ico)
ENDIF(Q_WS_WIN)

set(DEST_DIR ".")
if(UNIX AND NOT APPLE)
    set(DEST_DIR "bin")
endif()

# Clear the file first otherwise we get duplicates
file(WRITE ${PROJECT_BINARY_DIR}/Qt_Plugins.txt "")
FindQt4Plugins("qgif;qjpeg;qtiff" "${PROJECT_BINARY_DIR}/Qt_Plugins.txt"
                "${PROJECT_BINARY_DIR}/LibSearchDirs.txt"
                "imageformats" )

file(READ ${PROJECT_BINARY_DIR}/Qt_Plugins.txt QT_PLUGINS)
BuildQtAppBundle(
    TARGET ${PROJECT_NAME}
    SOURCES ${${PROJECT_NAME}_PROJECT_SRCS}
    DEBUG_EXTENSION ${EXE_DEBUG_EXTENSION}
    ICON_FILE  ${APPLICATION_ICON}
    VERSION_MAJOR ${QHAADFGui_VER_MAJOR}
    VERSION_MINOR ${QHAADFGui_VER_MINOR}
    VERSION_PATCH ${QHAADFGui_VER_PATCH}
    BINARY_DIR    ${${PROJECT_NAME}_BINARY_DIR}
    LINK_LIBRARIES ${QT_LIBRARIES} QtSupport MBIRLib
    LIB_SEARCH_DIRS ${CMAKE_LIBRARY_OUTPUT_DIRECTORY} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
    QT_PLUGINS    ${QT_PLUGINS}
#        OTHER_PLUGINS ${OTHER_PLUGINS}
    COMPONENT     Applications
    INSTALL_DEST  ${DEST_DIR}
)


